plugins {
    id 'java-library'
    id "java-test-fixtures"
    id 'idea'
    id 'maven-publish'

    // todo move to all projects or subprojects.  possibly only include with java projects
    id 'jacoco'
    id "com.github.spotbugs"
}

group 'org.apache.cassandra.sidecar'
version project.version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation("org.slf4j:slf4j-api:${project.slf4jVersion}")

    implementation(project(":common"))
    implementation(project(":adapters:base"))

    compileOnly('org.jetbrains:annotations:23.0.0')
    compileOnly('com.google.code.findbugs:jsr305:3.0.2') // required for the @NotThreadSafe annotation
    compileOnly(group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.11.3')

    implementation('org.mockito:mockito-inline:4.10.0')
    implementation("org.assertj:assertj-core:3.24.2")
    implementation("com.google.guava:guava:${project.rootProject.guavaVersion}")
    implementation("org.junit.jupiter:junit-jupiter-api:${project.junitVersion}")
    implementation("org.junit.jupiter:junit-jupiter-params:${project.junitVersion}")
    implementation("org.junit.jupiter:junit-jupiter-engine:${project.junitVersion}")
    implementation("org.apache.cassandra:dtest-api:0.0.15")
    implementation("org.junit.vintage:junit-vintage-engine:${junitVersion}")

    implementation(rootProject.files("dtest-jars/${dtestJar}"))
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId "${archivesBaseName}"
            version System.getenv("CODE_VERSION") ?: "${version}"
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
